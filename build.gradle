plugins {
  id 'java'
  id 'fabric-loom' version '1.6-SNAPSHOT'
}

group = project.maven_group
version = project.mod_version

repositories {
  maven { url = "https://maven.fabricmc.net/" }                    // Fabric
  maven { url = "https://api.modrinth.com/maven" }                 // SVC mod (dev runtime)
  maven { url = "https://maven.maxhenkel.de/repository/public" }   // SVC API
  mavenCentral()
}

dependencies {
  minecraft "com.mojang:minecraft:${minecraft_version}"
  mappings loom.officialMojangMappings()
  modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
  modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_api_version}"

  // Simple Voice Chat - API is a plain library; bring the mod at dev runtime so addon entrypoint loads
  implementation "de.maxhenkel.voicechat:voicechat-api:${svc_version}"
  modRuntimeOnly "maven.modrinth:simple-voice-chat:fabric-${svc_version}"
}

java {
  toolchain { languageVersion = JavaLanguageVersion.of(17) }
  withSourcesJar()
}

loom {
  runs {
    client {
      client()
      setConfigName("YellSpells Client")
      ideConfigGenerated(true)
    }
    server {
      server()
      setConfigName("YellSpells Server")
      ideConfigGenerated(true)
    }
  }
}

tasks.withType(JavaCompile).configureEach {
  options.release = 17
  options.encoding = "UTF-8"
}

processResources {
  inputs.property "version", project.version
  filesMatching("fabric.mod.json") {
    expand project.properties
  }
}
