plugins {
  id 'java'
  id 'fabric-loom' version '1.11.5'
}

group = project.maven_group
version = project.mod_version

repositories {
    maven { name = "Fabric";    url = "https://maven.fabricmc.net/" }
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
        content { includeGroup "maven.modrinth" }
    }
    maven { name = "MaxHenkel"; url = "https://maven.maxhenkel.de/repository/public" } // SVC API
    mavenCentral()
}

dependencies {
    // Fabric
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

    // Minecraft
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings "net.fabricmc:yarn:${yarn_mappings}:v2"

    // Simple Voice Chat API and dev runtime mod
    implementation "de.maxhenkel.voicechat:voicechat-api:${voicechat_api_version}"
    modRuntimeOnly "maven.modrinth:simple-voice-chat:fabric-${voicechat_version}"
}

java {
  toolchain { languageVersion = JavaLanguageVersion.of(21) }
  withSourcesJar()
}

loom {
  runs {
    client {
      client()
      setConfigName("YellSpells Client")
      ideConfigGenerated(true)
    }
    server {
      server()
      setConfigName("YellSpells Server")
      ideConfigGenerated(true)
    }
  }
}

tasks.withType(JavaCompile).configureEach {
  options.release = 21
  options.encoding = "UTF-8"
}

processResources {
  inputs.property "version", project.version
  filesMatching("fabric.mod.json") {
    expand project.properties
  }
}
